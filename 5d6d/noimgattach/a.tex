\documentclass[a4paper]{article}
\usepackage{amsmath}
\usepackage{amssymb}
\usepackage{amsthm}
\usepackage{float}
\usepackage{xeCJK}
\usepackage{fancyhdr}
\usepackage[top=1in,bottom=1in,left=0.8in,right=0.8in]{geometry}
\usepackage{asymptote}

\setmainfont{CMU Serif}
\setsansfont{CMU Sans Serif}
\setmonofont{CMU Typewriter Text}

\setCJKmainfont[BoldFont=SimHei,ItalicFont=KaiTi]{SimSun}
\setCJKfamilyfont{song}[BoldFont=SimHei,ItalicFont=KaiTi]{SimSun}
\newcommand\song{\CJKfamily{song}}

\renewcommand{\figurename}{图}

\begin{document}

\begin{figure}[H]
\begin{minipage}[b]{0.333\linewidth}
\centering
\begin{asy}
size(150);
import graph3;
int n=3;
real C0=sqrt(6);
real C1=2*sqrt(2);
triple[] V={(-C0/3,C1/6,-1/3),(C0/3,C1/6,-1/3),(0,-C1/3,-1/3)};
triple U=(0,0,1);
int i;
for(i=0;i<n;++i)
{
	draw(V[i]--V[(i+1)%n]);
	draw(V[i]--U);
}
for(i=0;i<n;++i)
{
	draw(surface(V[i]--V[(i+1)%n]--U--cycle),red);
}
draw(surface(V[0]--V[1]--V[2]--cycle),red);
\end{asy}
\caption{正四面体}
\end{minipage}%
\begin{minipage}[b]{0.333\linewidth}
\centering
\begin{asy}
size(150);
import graph3;
int n=4;
triple[] V={(0.5,0.5,0.5),(-0.5,0.5,0.5),(-0.5,-0.5,0.5),(0.5,-0.5,0.5)};
triple[] U={(0.5,0.5,-0.5),(-0.5,0.5,-0.5),(-0.5,-0.5,-0.5),(0.5,-0.5,-0.5)};
int i;
for(i=0;i<n;++i)
{
	draw(V[i]--V[(i+1)%4]);
	draw(U[i]--U[(i+1)%4]);
	draw(V[i]--U[i]);
}
for(i=0;i<n;++i)
{
	draw(surface(V[i]--V[(i+1)%n]--U[(i+1)%n]--U[i]--cycle),green);
}
draw(surface(V[0]--V[1]--V[2]--V[3]--cycle),green);
draw(surface(U[0]--U[1]--U[2]--U[3]--cycle),green);
\end{asy}
\caption{正方体}
\end{minipage}%
\begin{minipage}[b]{0.333\linewidth}
\centering
\begin{asy}
size(150);
import graph3;
int n=4;
real C0=sqrt(2)/2;
triple[] V={(C0,0,0),(0,C0,0),(-C0,0,0),(0,-C0,0)};
triple[] U={(0,0,C0),(0,0,-C0)};
int i;
for(i=0;i<n;++i)
{
	draw(V[i]--V[(i+1)%n]);
	draw(V[i]--U[0]);
	draw(V[i]--U[1]);
}
for(i=0;i<n;++i)
{
	draw(surface(U[0]--V[i]--V[(i+1)%n]--cycle),red);
	draw(surface(U[1]--V[i]--V[(i+1)%n]--cycle),red);
}
\end{asy}
\caption{正八面体}
\end{minipage}%
\end{figure}

\begin{figure}[H]
\begin{minipage}[b]{0.5\linewidth}
\centering
\begin{asy}
size(150);
import graph3;
int n=5;
real t=pi/n;
real r=sqrt(50+10*sqrt(5))/10;
real R=sqrt(25+10*sqrt(5))/5;
real H=sqrt(250+110*sqrt(5))/20;
real h=(sqrt(5)-2)*H;
real p=(sqrt(3)+sqrt(15))/4;
r=r/p;
R=R/p;
H=H/p;
h=h/p;
triple[] A;
triple[] B;
triple[] C;
triple[] D;
int i;
for(i=0;i<n;++i)
{
	A.push((r*cos(2*i*t),r*sin(2*i*t),H));
	B.push((R*cos(2*i*t),R*sin(2*i*t),h));
	C.push((R*cos(2*i*t+t),R*sin(2*i*t+t),-h));
	D.push((r*cos(2*i*t+t),r*sin(2*i*t+t),-H));
}
for(i=0;i<n;++i)
{
	draw(A[i]--A[(i+1)%n]);
	draw(A[i]--B[i]);
	draw(B[i]--C[i]);
	draw(C[i]--B[(i+1)%n]);
	draw(D[i]--D[(i+1)%n]);
	draw(C[i]--D[i]);
}
for(i=0;i<n;++i)
{
	draw(surface(A[i]--A[(i+1)%n]--B[(i+1)%n]--C[i]--B[i]--cycle),blue);
	draw(surface(D[i]--D[(i+1)%n]--C[(i+1)%n]--B[(i+1)%n]--C[i]--cycle),blue);
}
draw(surface(A[0]--A[1]--A[2]--A[3]--A[4]--cycle),blue);
draw(surface(D[0]--D[1]--D[2]--D[3]--D[4]--cycle),blue);
\end{asy}
\centering
\caption{正十二面体}
\end{minipage}%
\begin{minipage}[b]{0.5\linewidth}
\centering
\begin{asy}
size(150);
import graph3;
int n=5;
real t=pi/n;
real r=sqrt(50+10*sqrt(5))/10;
real H=sqrt(10+2*sqrt(5))/4;
real h=sqrt(50+10*sqrt(5))/20;
r=r/H;
h=h/H;
H=1;
triple[] A;
triple[] B;
triple[] C={(0,0,H),(0,0,-H)};
int i;
for(i=0;i<n;++i)
{
	A.push((r*cos(2*i*t),r*sin(2*i*t),h));
	B.push((r*cos(2*i*t+t),r*sin(2*i*t+t),-h));
}
for(i=0;i<n;++i)
{
	draw(C[0]--A[i]);
	draw(A[i]--A[(i+1)%n]);
	draw(A[i]--B[i]);
	draw(B[i]--A[(i+1)%n]);
	draw(B[i]--B[(i+1)%n]);
	draw(B[i]--C[1]);
}
for(i=0;i<n;++i)
{
	draw(surface(C[0]--A[i]--A[(i+1)%n]--cycle),red);
	draw(surface(A[i]--A[(i+1)%n]--B[i]--cycle),red);
	draw(surface(A[(i+1)%n]--B[i]--B[(i+1)%n]--cycle),red);
	draw(surface(B[i]--B[(i+1)%n]--C[1]--cycle),red);
}
\end{asy}
\caption{正二十面体}
\end{minipage}%
\end{figure}

\begin{figure}[H]
\begin{minipage}[b]{0.5\linewidth}
\centering
\begin{asy}
size(140);
import graph3;
int n=5;
int m=2;
real r=1/(2*sin(m*pi/n));
real t=2*pi/n;
real h=sqrt(4-1/(sin(m*t/4))^2)/2;
real R=sqrt(r^2+(h/2)^2);
r=r/R;
h=h/R;
triple[] V;
int i;
for(i=0;i<2*n;++i)
{
    V.push((r*cos(i*t/2),r*sin(i*t/2),-h/2));
}
for(i=0;i<2*n;++i)
{
    V.push((r*cos(i*t/2),r*sin(i*t/2),h/2));
}
for(i=0;i<2*n;++i)
{
    if(i%2==0)
    {
        draw(V[i]--V[(i+2*(n-m))%(2*n)]);
        draw(V[(i+1)%(2*n)+2*n]--V[((i+1)%(2*n)+2*(n-m))%(2*n)+2*n]);
        draw(V[i]--V[(i+n-m)%(2*n)+2*n]);
        draw(V[(i+1)%(2*n)+2*n]--V[((i+1)%(2*n)+n-m)%(2*n)]);
    }
}
for(i=0;i<2*n;++i)
{
    if(i%2==0)
    {
        draw(surface(V[i]--V[(i+2*(n-m))%(2*n)]--V[(i+n-m)%(2*n)+2*n]--cycle),red);
    }
    else
    {
        draw(surface(V[i+2*n]--V[(i+2*(n-m))%(2*n)+2*n]--V[(i+n-m)%(2*n)]--cycle),red);
    }
}
guide3 f;
guide3 g;
for(i=0;i<n;++i)
{
	f=f--V[(2*m*i)%(2*n)];
	g=g--V[(2*m*i+1)%(2*n)+2*n];
}
f=f--cycle;
g=g--cycle;
draw(surface(f),mediumblue);
draw(surface(g),mediumblue);
\end{asy}
\caption{正五角星交错棱柱}
\end{minipage}%
\begin{minipage}[b]{0.5\linewidth}
\centering
\begin{asy}
size(140);
import graph3;
int n=7;
int m=3;
int m1=(int)((n-m)/2);
real r=1/(2*sin(m*pi/n));
real t=2*pi/n;
real h=sqrt(4-1/(sin(m*t/4))^2)/2;
real R=sqrt(r^2+(h/2)^2);
r=r/R;
h=h/R;
triple[] V;
int i;
for(i=0;i<n;++i)
{
    V.push((r*cos(i*t),r*sin(i*t),-h/2));
}
for(i=0;i<n;++i)
{
    V.push((r*cos(i*t),r*sin(i*t),h/2));
}
for(i=0;i<n;++i)
{
    draw(V[i]--V[(i+m)%n]);
    draw(V[i+n]--V[(i+m)%n+n]);
    draw(V[i]--V[(i+m1)%n+n]);
    draw(V[i+n]--V[(i+m1)%n]);
}
for(i=0;i<n;++i)
{
    draw(surface(V[i]--V[(i+m)%n]--V[(i+m+m1)%n+n]--cycle),red);
    draw(surface(V[i+n]--V[(i+m)%n+n]--V[(i+m+m1)%n]--cycle),red);
}
guide3 f;
guide3 g;
for(i=0;i<n;++i)
{
	f=f--V[(m*i)%n];
	g=g--V[(m*i)%n+n];
}
f=f--cycle;
g=g--cycle;
draw(surface(f),mediumyellow);
draw(surface(g),mediumyellow);
\end{asy}
\caption{正七角星交错棱柱}
\end{minipage}%
\end{figure}

\end{document}